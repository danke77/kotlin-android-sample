apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    def globalConfig = rootProject.extensions.getByName("ext")

    compileSdkVersion globalConfig.getAt("androidCompileSdkV")
    buildToolsVersion globalConfig.getAt("androidBuildToolsV")

    defaultConfig {
        applicationId "${appId}"
        minSdkVersion globalConfig.getAt("androidMinSdkV")
        targetSdkVersion globalConfig.getAt("androidTargetSdkV")
        versionCode "${VERSION_CODE}" as int
        versionName "${VERSION_NAME}"
    }

    signingConfigs {
        release {
            def storeFileValue = ""
            def storePasswordValue = ""
            def keyAliasValue = ""
            def keyPasswordValue = ""

            if (new File('local.properties').exists()) {
                Properties properties = new Properties()
                properties.load(project.rootProject.file('local.properties').newDataInputStream())
                storeFileValue = properties.getProperty("store.file")
                storePasswordValue = properties.getProperty("store.password")
                keyAliasValue = properties.getProperty("key.alias")
                keyPasswordValue = properties.getProperty("key.password")
            }

            storeFile file("${storeFileValue}")
            storePassword "${storePasswordValue}"
            keyAlias "${keyAliasValue}"
            keyPassword "${keyPasswordValue}"
        }
        debug {
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    dexOptions {
        incremental true
        javaMaxHeapSize "2048M"
    }

    lintOptions {
        abortOnError false
    }

    sourceSets {
        main {
            manifest.srcFile 'src/main/AndroidManifest.xml'
            java.srcDirs = ['src/main/java', '.apt_generated']
            aidl.srcDirs = ['src/main/aidl', '.apt_generated']
            assets.srcDirs = ['src/main/assets']
            res.srcDirs =
                    [
                            'src/main/res/layouts/common',
                            'src/main/res/layouts/component',
                            'src/main/res/layouts/login',
                            'src/main/res/layouts/main',
                            'src/main/res/layouts/search',
                            'src/main/res/layouts/user',
                            'src/main/res/layouts',
                            'src/main/res'
                    ]
        }
        main.java.srcDirs += 'src/main/kotlin'
    }

    packagingOptions {
        exclude('META-INF/LICENSE.txt')
        exclude('META-INF/NOTICE.txt')
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    def androidLibs = rootProject.ext.androidDependencies
    compile androidLibs.appcompatV7
    compile androidLibs.supportV13
    compile androidLibs.supportDesign
    compile androidLibs.cardview

    def kotlinLibs = rootProject.ext.kotlinDependencies
    compile kotlinLibs.kotlinStdlib
    compile kotlinLibs.ankoCommon
    compile kotlinLibs.ankoSqlite

    def thirdLibs = rootProject.ext.thirdDependencies
    compile thirdLibs.gson
    compile thirdLibs.glide
    compile thirdLibs.titan
    compile thirdLibs.circleIamgeView
    compile thirdLibs.materialEdittext
    compile thirdLibs.materialDialog
    compile thirdLibs.easypermissions
    debugCompile thirdLibs.leakcanaryDebug
    releaseCompile thirdLibs.leakcanaryRelease

    def testLibs = rootProject.ext.testDependencies
    testCompile testLibs.junit
}
